# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(libblkid libblkid-dev libmount libmount-dev libuuid libuuid-dev)
pkgver=2.29.2
pkgrel=1
pkgdesc='Miscellaneous utilities for Linux'
arch=(x86_64)
url=http://freecode.com/projects/util-linux/
license=(GPL)
groups=(base)
depends=()
makedepends=(libz-dev)
options=()
changelog=ChangeLog
source=(
    "https://www.kernel.org/pub/linux/utils/util-linux/v2.29/util-linux-${pkgver}.tar.xz"
)

sha256sums=(
    accea4d678209f97f634f40a93b7e9fcad5915d1f4749f6c47bee6bf110fe8e3
)

build() {
    cd "${srcdir}/util-linux-${pkgver}"
    CFLAGS+=' -fPIC' ./configure --prefix=''
    make
}

package_libblkid() {
    pkgfiles=(
        lib/libblkid*.so.*
    )
    depends=(musl libuuid)
    cd "${srcdir}/util-linux-${pkgver}"
    MAKEFLAGS='' make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libblkid-dev() {
    pkgfiles=(
        include/blkid
        lib/pkgconfig/blkid.pc
        lib/libblkid.a
        lib/libblkid.so
    )
    depends=(libblkid)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libmount() {
    pkgfiles=(
        lib/libmount*.so.*
    )
    depends=(musl libblkid)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libmount-dev() {
    pkgfiles=(
        include/libmount
        lib/pkgconfig/mount.pc
        lib/libmount.a
        lib/libmount.so
    )
    depends=(libmount)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libuuid() {
    pkgfiles=(
        lib/libuuid*.so.*
    )
    depends=(musl)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libuuid-dev() {
    pkgfiles=(
        include/uuid
        lib/pkgconfig/uuid.pc
        lib/libuuid.a
        lib/libuuid.so
    )
    depends=(libuuid)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
