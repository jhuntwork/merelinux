# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(execline execline-dev)
pkgver=2.1.5.0
pkgrel=1
pkgdesc='a (non-interactive) scripting language.'
arch=(x86_64)
url='http://skarnet.org/software/execline/'
license=(ISC)
groups=(base)
depends=()
makedepends=(skalibs-dev)
options=()
changelog=ChangeLog
source=(
    "http://skarnet.org/software/execline/execline-${pkgver}.tar.gz"
)

sha256sums=(
    8a3605a6db73183baa376bf2130e8b7eb75a5b5347c7f9144e520fe86bcb1142
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's@#!/bin/sh@#!/bin/bash@' configure
    ./configure \
      --prefix=/ \
      --enable-static-libc
    make
}

package_execline() {
    options=(upx)
    pkgfiles=(
        bin
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_execline-dev() {
    pkgfiles=(
        include
        lib
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    mv lib/execline/libexecline.a lib
    rm -rf lib/execline
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
