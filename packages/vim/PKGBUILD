# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=vim
_vermajor=8
_verminor=0
_verpatch=0891
pkgver="${_vermajor}.${_verminor}.${_verpatch}"
pkgrel=1
pkgdesc='An IMproved version of the vi editor'
arch=(x86_64)
url='http://www.vim.org'
license=(VIM)
groups=(base)
depends=()
makedepends=(libncurses-dev)
options=()
changelog=ChangeLog
source=(
    "https://github.com/vim/vim/archive/v${pkgver}.tar.gz"
    https://projects.archlinux.org/pacman.git/plain/contrib/PKGBUILD.vim?id=ec01a22f7d13147a52ac8c0c1ee1b5fc2426de39
    vimrc
)

sha256sums=(
    0d1467df73fbfb57a48b728b5a6b1ac1e50d062b9b09471c59c140c2eb9b4384
    dda73393c44e448ddeddaff76287dbbe5fcea1bc381c5d065789bd3c1b481f0e
    5e698ff1872bae79b7eae3f35bfca2e426c2698a785687fb9035e0fee16de91a
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"

    # Set the path of the default vimrc file
    echo '#define SYS_VIMRC_FILE "/etc/vimrc"' >> src/feature.h

    # Fix some feature tests
    sed -i -e '/thisterminaldoesnotexist/i #include <term.h>' \
           -e '/0xffffffffUL;/i #include <stdlib.h>' \
	   src/auto/configure

    CC='gcc -I/include/ncursesw' \
        CFLAGS="${CFLAGS} -fPIC" \
        LDFLAGS='-Wl,-static' \
        ./configure \
        --prefix='' \
        --enable-multibyte \
        --with-tlib=ncursesw
    make
}

package_vim() {
    pkgfiles=(
        bin
        etc
        share/man/man1
        share/vim
    )
    options+=(emptydirs)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    install -d etc
    install -m 0644 "${srcdir}/vimrc" etc/vimrc
    install -m 0644 "${srcdir}/PKGBUILD.vim?id=ec01a22f7d13147a52ac8c0c1ee1b5fc2426de39" \
      "share/vim/vim${_vermajor}${_verminor}/syntax/PKGBUILD.vim"
    echo "au BufNewFile,BufRead PKGBUILD set filetype=PKGBUILD" \
      >> "share/vim/vim${_vermajor}${_verminor}/filetype.vim"
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
