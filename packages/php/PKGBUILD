# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(php php-fpm php-dev)
pkgver=5.6.29
_memcachedver=2.2.0
_mongover=1.6.14
_yamlver=1.2.0
_redisver=2.2.8
pkgrel=2
pkgdesc='A scripting language widely used in web development'
arch=(x86_64)
url='http://www.php.net'
license=(PHP)
groups=(base)
depends=()
makedepends=(
    bison
    gcc-c++
    gettext
    libcurl-dev
    libmemcached-dev
    libressl-dev
    libtool
    libxml2-dev
    libyaml-dev
    libz-dev
    pkgconf
)
options=()
changelog=ChangeLog

source=(
    "http://php.net/distributions/php-${pkgver}.tar.bz2"
    "https://pecl.php.net/get/memcached-${_memcachedver}.tgz"
    "https://pecl.php.net/get/mongo-${_mongover}.tgz"
    "https://pecl.php.net/get/yaml-${_yamlver}.tgz"
    "https://pecl.php.net/get/redis-${_redisver}.tgz"
    php-fpm-service
    php-fpm-log
    php-fpm.install
    php-fpm.conf
    php.ini
    fix_expr_syntax.patch
)
sha256sums=(
    499b844c8aa7be064c111692e51a093ba94e54d2d9abb01e70ea76183a1825bb
    17b9600f6d4c807f23a3f5c45fcd8775ca2e61d6eda70370af2bef4c6e159f58
    586a0f55d29198010da5f4c932a183491f114db6e1b0ba8e40e7246b1a4a96d0
    fdf0eec6bf16b0c45e58076a2048f96140a778463724fb82ab49ab5fb4c7e19a
    fc533752de62848f645707b65cba9d015ab4ed9c7a978c7bd710a5934d97b1b5
    d78dbfab9328333f48dd657ffec39637fe12774431565259e126088e5d49fa9a
    e622dba8b94208a0313f5a1951928fc9bdb27dd56b3a56e3d40afe667e4e7506
    f4947a011d1c5f318eb7933c3ab08fa48cc8c28a61faba70447f6e911354544a
    693a0376eb4c2dc8bfe8027cec0f2eb2982ea64cfd3b36d4f089d4fe464dbfff
    8897e31325d4ee3c2a1a3fa81a415f59092b5e72ea8887ea501b472c6d0b6cf3
    291be80b93513f734eb4df8d0a6225890e48f74c3dd32d3f4889ce2aa27b23d0
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # Fix uses of expr in configure which aren't portable
    patch -Np1 -i "${srcdir}/fix_expr_syntax.patch"

    # Fix warnings about including sys/poll.h
    sed -i '/sys\/poll.h/s@sys/@@' main/php_network.h \
      sapi/cgi/fastcgi.c main/network.c sapi/fpm/fpm/fastcgi.c

    # Fix implicit declaration in mbstring
    sed -i \
        "/endif/s@^.*\$@int mbfl_filt_put_invalid_char(int c, mbfl_convert_filter *filter);\n&@" \
        ext/mbstring/libmbfl/filters/mbfilter_utf8_mobile.h

    # Fix readdir tests on musl-based system
    sed -i "/dirent.h/s@^.*\$@#include <stdlib.h>\n&@" aclocal.m4

    # memcached ext
    mv "${srcdir}/memcached-${_memcachedver}" ext/memcached

    # mongo ext
    mv "${srcdir}/mongo-${_mongover}" ext/mongo
    # Fix namespace collision with ext/hash
    sed -i -e 's@php_hash_copy(@php_hash_copy_m(@' \
           -e 's@php_hash_copy,@php_hash_copy_m,@' \
           ext/mongo/contrib/crypto.c

    # redis ext
    mv "${srcdir}/redis-${_redisver}" ext/redis

    # yaml ext
    mv "${srcdir}/yaml-${_yamlver}" ext/yaml

    rm configure
    ./buildconf --force

    sed -i 's@CURL_CONFIG --libs@CURL_CONFIG --static-libs@' configure
    ./configure \
      --prefix=/ \
      --with-config-file-path=/etc/php \
      --disable-all \
      --disable-cgi \
      --disable-shared \
      --enable-static \
      --enable-cli \
      --enable-ctype \
      --enable-filter \
      --enable-fpm \
      --enable-hash \
      --enable-json \
      --enable-libxml \
      --enable-mbstring \
      --enable-mbregex \
      --enable-memcached \
      --disable-memcached-sasl \
      --enable-mongo \
      --enable-pear \
      --enable-pdo \
      --enable-phar \
      --enable-redis \
      --enable-session \
      --enable-simplexml \
      --enable-tokenizer \
      --enable-xml \
      --with-curl \
      --with-iconv \
      --with-gettext \
      --with-mysql=mysqlnd \
      --with-mysqli=mysqlnd \
      --with-pdo-mysql=mysqlnd \
      --with-openssl \
      --with-yaml \
      --with-zlib \
      --with-pic \
      ac_cv_crypt_des=no \
      ac_cv_crypt_ext_des=no
    make
}

check() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make test
}

package_php() {
    pkgfiles=(
        bin/phar
        bin/phar.phar
        bin/php
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make INSTALL_ROOT="${pkgdirbase}/destdir" install
    cd "${pkgdirbase}/destdir"
    # Fix broken symlink
    rm bin/phar
    ln -s phar.phar bin/phar
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_php-fpm() {
    pkgfiles=(
        etc
        sbin
    )
    backup=(
        etc/php-fpm.conf
        etc/php.ini
    )
    install=php-fpm.install

    # Service files
    install -d "${pkgdir}/etc/s6/services/available/php-fpm/log"
    install -m 0754 "${srcdir}/php-fpm-service" \
        "${pkgdir}/etc/s6/services/available/php-fpm/run"
    install -m 0754 "${srcdir}/php-fpm-log" \
        "${pkgdir}/etc/s6/services/available/php-fpm/log/run"

    # Configuration
    install -m 0644 "${srcdir}/php-fpm.conf" \
        "${pkgdir}/etc/php-fpm.conf"
    install -m 0644 "${srcdir}/php.ini" \
        "${pkgdir}/etc/php.ini"

    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_php-dev() {
    depends=(
        php
    )
    pkgfiles=(
        bin/php-config
        bin/phpize
        include
        lib/php/build
    )

    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
