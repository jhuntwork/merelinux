# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(libressl libressl-dev ca-certs)
pkgver=2.5.2
pkgrel=1
pkgdesc='A fork of OpenSSL 1.0.1g with the goal modernizing the codebase and improving security'
arch=(x86_64)
url='http://www.libressl.org'
license=(OpenSSL SSLeay)
groups=(base)
depends=()
makedepends=(libtool)
options=()
changelog=ChangeLog

source=(
    "http://ftp.openbsd.org/pub/OpenBSD/LibreSSL/libressl-${pkgver}.tar.gz"
)
sha256sums=(
    0ffa7d70809284a4ac96e965918a61c1d7930bca865457a7db0ff0afc8201c82
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    autoreconf -i
    CFLAGS='-fPIC' ./configure --prefix=''
    make V=1
}

check() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make check
    make DESTDIR="$(pwd)/.destdir" install
}

package_libressl() {
    pkgfiles=(
        bin
        etc/ssl/*.cnf
        "lib/lib*.so.*"
    )
    depends=(musl)

    cd "${srcdir}/${pkgbase}-${pkgver}/.destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_libressl-dev() {
    pkgfiles=(
        include
        "lib/*.a"
        "lib/*.so"
        lib/pkgconfig
    )
    depends=(libressl)

    cd "${srcdir}/${pkgbase}-${pkgver}/.destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_ca-certs() {
    pkgfiles=(
        etc/ssl/ca-certs.pem
    )
    cd "${srcdir}/${pkgbase}-${pkgver}/.destdir"
    mv etc/ssl/cert.pem etc/ssl/ca-certs.pem
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
