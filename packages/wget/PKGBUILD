# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=wget
pkgver=1.18
pkgrel=1
pkgdesc='An HTTP, HTTPS and FTP file retriever'
arch=(x86_64)
url='http://www.gnu.org/software/wget'
license=(GPL3)
groups=(base)
depends=()
makedepends=(
    libressl-dev
    pkgconf
    zlib-dev
)
options=(upx)
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"
)

sha256sums=(
    b5b55b75726c04c06fe253daec9329a6f1a3c0c1878e3ea76ebfebc139ea9cc1
)

backup=(etc/wgetrc)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    LDFLAGS="-Wl,-static" \
      ./configure \
      --prefix='' \
      --with-ssl=openssl
    make $MAKEFLAGS
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" install
    rm -rf "${pkgdir}/lib"
    rm -rf "${pkgdir}/share"
    echo "ca-certificate = /etc/ssl/ca-certs.pem" >>"${pkgdir}/etc/wgetrc"
}
