pkgname=wget
pkgver=1.16.3
pkgrel=1
pkgdesc='An HTTP, HTTPS and FTP file retriever'
arch=(x86_64)
url='http://www.gnu.org/software/wget'
license=(GPL3)
groups=(base)
depends=()
makedepends=(
    openssl-dev
    pkgconf
    zlib-dev
)
options=()
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"
)

sha256sums=(
    67f7b7b0f5c14db633e3b18f53172786c001e153d545cfc85d82759c5c2ffb37
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    LDFLAGS="-Wl,-static" \
      ./configure \
      --prefix='' \
      --with-ssl=openssl
    make $MAKEFLAGS
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" install
    rm -rf "${pkgdir}/lib"
    rm -rf "${pkgdir}/share"
    echo "ca-certificate = /etc/certs/ca-bundle.crt" >>"${pkgdir}/etc/wgetrc"
}
