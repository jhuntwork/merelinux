pkgname=dropbear
pkgver=2015.68
pkgrel=1
pkgdesc='A relatively small SSH server and client.'
arch=(x86_64)
url='http://matt.ucc.asn.au/dropbear/dropbear.html'
license=(MIT)
groups=(base)
depends=(s6 execline)
makedepends=(zlib-dev)
options=()
changelog=ChangeLog
source=(
    "http://matt.ucc.asn.au/dropbear/releases/dropbear-${pkgver}.tar.bz2"
    dropbear-service
    dropbear-log
    dropbear.install
)

sha256sums=(
    55ea7c1e904ffe4b1cdbe1addca8291a2533d7d285fd22ac33608e9502a62446
    39dd2592dc121badf5b50ab71d56045e88c314182f9c7c12812296ab4d9c5362
    df91dcee399c728a884f89723f29754353710b0e6c9ef8e8d1ae87300928b0f8
    5103554505f9642e9b5763842db6a18e3826b8bf6cb34edd2d5afcaa280c2999
)

install=dropbear.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    LDFLAGS='-Wl,-static' ./configure \
      --prefix='' \
      --disable-wtmp
    make $MAKEFLAGS
}

package() {
    options+=(emptydirs)
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" install
    ln -s dbclient "${pkgdir}/bin/ssh"
    install -d -m 0755 "${pkgdir}/etc/dropbear"
    install -d "${pkgdir}/etc/s6-services/available/dropbear/log"
    install -m 0754 "${srcdir}/dropbear-service" \
        "${pkgdir}/etc/s6-services/available/dropbear/run"
    install -m 0754 "${srcdir}/dropbear-log" \
        "${pkgdir}/etc/s6-services/available/dropbear/log/run"
    rm -rf "$pkgdir"/share
}
