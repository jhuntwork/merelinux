# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(linux linux-headers)
pkgver=4.12.9
pkgrel=3
pkgdesc='System kernel'
arch=(x86_64)
url='http://www.kernel.org'
license=(GPL2)
groups=(base)
depends=()
makedepends=(bc perl)
options=()
changelog=ChangeLog
source=(
    "https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.xz"
    musl-compatibility.patch
    linux-config
)

sha256sums=(
    690439175c9dfbb90159bdc4b24cf10011675392264764f2031eb19ce0a1649c
    a43487eb727cfc14576084a437fab09fd79247f6fecce618a6254d8a4e2d4035
    e69f00ffe1b34e98dc256b930f1073fb7da7b567687d31dcd785e7ee5c85818f
)

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    cp "${srcdir}/linux-config" .config
    if [ -n "$DISTCC_HOSTS" ] ; then
        make CC='distcc gcc'
    else
        make
    fi
}

package_linux() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -d "${pkgdir}/boot"
    install "arch/${CARCH}/boot/bzImage" "${pkgdir}/boot/vmlinux"
}

package_linux-headers() {
    pkgdesc='System headers'
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make mrproper
    patch -Np1 -i "${srcdir}/musl-compatibility.patch"
    make INSTALL_HDR_PATH=dest HOSTCFLAGS="-D_GNU_SOURCE" headers_install
    cd dest
    find include ! -type d -name "*.h" | cpio -dumpv "${pkgdir}"
}
