# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(bison bison-dev)
pkgver=3.0.4
pkgrel=1
pkgdesc='A general-purpose parser generator.'
arch=('x86_64')
url='http://www.gnu.org/software/bison/'
license=('GPL3')
groups=('base')
depends=(m4)
makedepends=(perl)
options=()
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"
)

sha256sums=(
    a72428c7917bdf9fa93cb8181c971b6e22834125848cf1d03ce10b1bb0716fe1
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    LDFLAGS="-Wl,-static" ./configure --prefix=''
    make
}

package_bison() {
    pkgfiles=(
        bin/bison
        bin/yacc
        share/bison
        share/man/man1/bison.1
        share/man/man1/yacc.1
        share/aclocal/bison-i18n.m4
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_bison-dev() {
    pkgfiles=(
        lib/liby.a
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
