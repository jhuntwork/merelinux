pkgname=(s6 s6-dev)
pkgver=2.2.4.3
pkgrel=1
pkgdesc='A small process supervision suite for UNIX.'
arch=(x86_64)
url='http://skarnet.org/software/s6/'
license=(ISC)
groups=(base)
depends=()
makedepends=(skalibs-dev execline-dev)
options=()
changelog=ChangeLog
source=(
    "http://skarnet.org/software/s6/s6-${pkgver}.tar.gz"
    init
    rc.init
    rc.shutdown
    s6-svscan-crash
    s6-svscan-finish
    s6-svscan-log
    service
    service_functions.sh
    taillog
)

sha256sums=(
    f584ec56d5f2a3a2d81698d5f744b9b64d8fb6c22b56649faa71f3b62da7db3f
    4378dec40d6f497c298cbe5b2bc6234f3354b8e91f0ea1c053039a352e431baa
    3ba6876fced96c6abbda10bcdbd4a4e07f2c47bad51f1ca6f2ccd6edd8009200
    4662c87f8e847bf1acaf2c424229feb02ced456c7b203484e4ce5f31c254fc05
    a90dd71977a0a223265ddda7713e8cd798d14d21a3b010c9ba69b8c76c483133
    a10793f4303640159919e3fae315561417c94fe102f08ab655c505fb279e5950
    95ecf8c0ae9a55b3f8743a0dfdc6fd6816e6abd9cfe9802eba3752635d0d0c41
    c7954598edb572bd776c55327ac9280ef64e864a68f923b45f02d0a3180df9e7
    b697670a2f44c85ee5d492510e5dfca64682a5c5e0e9b17533122774607a3dc2
    6fe869bfaf65ea4528bcb437c8c2d818aae898ea3be9215ae082ce3548e34d08
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's@#!/bin/sh@#!/bin/bash@' configure
    ./configure \
      --prefix=/ \
      --libexecdir=/lib/s6 \
      --enable-static-libc
    make $MAKEFLAGS
}

package_s6() {
    options+=(emptydirs)
    pkgfiles=(
        etc
        bin
        sbin
        lib/s6
    )
    depends=(skalibs-leapsecs)

    # Run make install
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    rm lib/s6/libs6.a
    mv bin/* sbin/

    # Services dir and init
    install -d etc/s6-services/{available,enabled}
    install -m 0750 "${srcdir}/init" sbin/init
    install -m 0750 "${srcdir}/rc.init" etc/rc.init
    install -m 0750 "${srcdir}/rc.shutdown" etc/rc.shutdown

    # Early env vars
    install -d etc/s6-env
    echo 'UTC' >etc/s6-env/TZ

    # crash and finish scripts for s6-svscan
    install -d etc/s6-init-services/.s6-svscan
    install -m 0750 "${srcdir}/s6-svscan-crash" etc/s6-init-services/.s6-svscan/crash
    install -m 0750 "${srcdir}/s6-svscan-finish" etc/s6-init-services/.s6-svscan/finish

    # Script and named pipe for catchall logging process
    install -d etc/s6-init-services/s6-svscan-log
    install -m 0750 "${srcdir}/s6-svscan-log" etc/s6-init-services/s6-svscan-log/run
    mkfifo etc/s6-init-services/s6-svscan-log/fifo
    chmod 0600 etc/s6-init-services/s6-svscan-log/fifo

    # Human readable/usable service compatibility layer
    install -m 0754 "${srcdir}/service_functions.sh" lib/s6/
    install -m 0754 "${srcdir}/service" sbin/
    install -m 0755 "${srcdir}/taillog" bin/

    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_s6-dev() {
    pkgfiles=(
        include
        lib/libs6.a
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    mv lib/s6/libs6.a lib/
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
