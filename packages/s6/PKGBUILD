# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(s6 s6-dev)
pkgver=2.3.0.0
pkgrel=6
pkgdesc='A small process supervision suite for UNIX.'
arch=(x86_64)
url='http://skarnet.org/software/s6/'
license=(ISC)
groups=(base)
depends=()
makedepends=(skalibs-dev execline-dev)
options=()
changelog=ChangeLog
source=(
    "http://skarnet.org/software/s6/s6-${pkgver}.tar.gz"
    init
    rc.init
    rc.shutdown
    s6-svscan-crash
    s6-svscan-finish
    s6-svscan-log
    service
    service_functions.sh
    taillog
    tty1-service
)

sha256sums=(
    49c9a0476d93c6279c776ca5ae9c49b3fd39c356eda674828aebf78f8488e8e6
    c01dc625a11f12dce8600382790dd08e557cd5aeddbc59eba2a621dcaf4e58e5
    321a1138500481920cd1f79056424000c85e3d820df41f4a09cf78a56e374140
    c9a2fd4b5a11bc2469ad3d574425c2cf5b4984673203ed110d0c2190216cd82f
    a90dd71977a0a223265ddda7713e8cd798d14d21a3b010c9ba69b8c76c483133
    9b3a7743208a3c65ce2fc63b8ad572a0f801be51e95052c46b3c1abeafdde650
    95ecf8c0ae9a55b3f8743a0dfdc6fd6816e6abd9cfe9802eba3752635d0d0c41
    a4ff5508a9a33a36f8d8ba0740aa42036f343e768f9b39834aa8e3395d6ea9fe
    61de2cb077920c14e9aad3c72d1689423acc153a1e045368836ecbb29cc7d122
    6fe869bfaf65ea4528bcb437c8c2d818aae898ea3be9215ae082ce3548e34d08
    538bc61ea2ef419c1e48e683d7b6a52ea45ea5b06ad765bbfa28b79d3953829f
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's@#!/bin/sh@#!/bin/bash@' configure
    ./configure \
      --prefix=/ \
      --libexecdir=/lib/s6 \
      --enable-static-libc
    make $MAKEFLAGS
}

package_s6() {
    options+=(emptydirs upx)
    pkgfiles=(
        etc
        bin
        sbin
        lib/s6
    )
    depends=(skalibs-leapsecs)

    # Run make install
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    rm lib/s6/libs6.a
    mv bin/* sbin/

    # Services dir and init
    install -d etc/s6/services/{available,enabled}
    install -m 0750 "${srcdir}/init" sbin/init
    install -m 0750 "${srcdir}/rc.init" etc/s6/rc.init
    install -m 0750 "${srcdir}/rc.shutdown" etc/s6/rc.shutdown

    # Early env vars
    install -d etc/s6/env
    echo 'UTC' >etc/s6/env/TZ

    # crash and finish scripts for s6-svscan
    install -d etc/s6/init-services/.s6-svscan
    install -m 0750 "${srcdir}/s6-svscan-crash" etc/s6/init-services/.s6-svscan/crash
    install -m 0750 "${srcdir}/s6-svscan-finish" etc/s6/init-services/.s6-svscan/finish

    # early tty1 service
    install -d etc/s6/init-services/tty1
    install -m 0754 "${srcdir}/tty1-service" etc/s6/init-services/tty1/run

    # Script and named pipe for catchall logging process
    install -d etc/s6/init-services/s6-svscan-log
    install -m 0750 "${srcdir}/s6-svscan-log" etc/s6/init-services/s6-svscan-log/run
    mkfifo etc/s6/init-services/s6-svscan-log/fifo
    chmod 0600 etc/s6/init-services/s6-svscan-log/fifo

    # Human readable/usable service compatibility layer
    install -m 0754 "${srcdir}/service_functions.sh" lib/s6/
    install -m 0754 "${srcdir}/service" sbin/
    install -m 0755 "${srcdir}/taillog" bin/

    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_s6-dev() {
    pkgfiles=(
        include
        lib/libs6.a
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    mv lib/s6/libs6.a lib/
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
