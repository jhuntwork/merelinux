# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='libarchive and pacman require nettle'
pkgname=(nettle nettle-dev)
pkgver=3.4.1
pkgrel=1
pkgdesc='A cryptographic library that is designed to fit easily in more or less any context'
arch=(x86_64)
url='http://www.lysator.liu.se/~nisse/nettle/'
license=(GPL3)
groups=(base)
depends=()
makedepends=(gmp-dev)
options=()
changelog=ChangeLog
source=(
    "https://ftp.gnu.org/gnu/nettle/nettle-${pkgver}.tar.gz"
)

sha256sums=(
    f941cf1535cd5d1819be5ccae5babef01f6db611f9b5a777bae9c7604b8a92ad
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    CFLAGS+=' -fPIC' ./configure --prefix='' \
        --enable-static
    make
}

package_nettle() {
    pkgfiles=(
        bin
        lib/libhogweed.so.*
        lib/libnettle.so.*
    )
    depends=(musl gmp)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_nettle-dev() {
    pkgfiles=(
        include
        lib/libhogweed.a
        lib/libhogweed.so
        lib/libnettle.a
        lib/libnettle.so
        lib/pkgconfig
    )
    depends=(nettle)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
