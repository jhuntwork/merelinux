# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(file file-dev)
pkgver=5.24
pkgrel=1
pkgdesc='The Fine Free File Command - a file type guesser'
arch=('x86_64')
url='http://darwinsys.com/file/'
license=('BSD')
groups=('base')
depends=()
makedepends=()
options=()
changelog=ChangeLog
source=(
    "ftp://ftp.astron.com/pub/file/${pkgname}-${pkgver}.tar.gz"
)

sha256sums=(
    802cb3de2e49e88ef97cdcb52cd507a0f25458112752e398445cea102bc750ce
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i 's/misc/file/' configure
    LDFLAGS="--static" ./configure \
      --prefix='' \
      --disable-shared \
      --enable-static
    make $MAKEFLAGS V=1
}

package_file() {
    options=(upx)
    pkgfiles=(
        'bin/file'
        'share/file/magic.mgc'
    )
    cd "$srcdir/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    printf "%s\n" ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_file-dev() {
    pkgfiles=(
        'include/*'
        'lib/*'
    )
    cd "$srcdir/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    printf "%s\n" ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
