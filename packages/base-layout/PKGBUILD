# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=base-layout
pkgver=1.7
pkgrel=1
pkgdesc='The base directory structure and a few core files for the system.'
arch=('x86_64')
url='http://www.merelinux.org'
license=(MIT)
groups=('base')
depends=()
makedepends=()
options=('emptydirs')
changelog=ChangeLog
source=(
    passwd
    group
    profile
    shells
    services
    protocols
)
sha256sums=(
    e1d32f445c3a6da06f9787ec7b8d59bb789dde4ad95e12b62ece5b68f307777e
    79aa3fb64f0b0d18ed4c53680d1fc991f7782dda9dcafc6bb592648056e21057
    2bdb6b6b41ea43b71fcd3b874a9ed1be1c55f101c9447bcd6059cd749fb3bb99
    6e7af7647d4064e6df502e1fa5f062a7ba753487a906850e9f332c6f0a7be7a3
    b4d36eb75767bebb41c5fa7a35599952e1883b3d3f6332496e1561eeb4067018
    297b784a25fc59641589c6ef05dc26680e2805e9cab37a4ea3699aa072a25c2e
)

backup=(
    etc/passwd
    etc/group
    etc/profile
    etc/shells
    etc/services
    etc/protocols
)

package() {
    install -d ${pkgdir}/bin
    install -d ${pkgdir}/boot
    install -d ${pkgdir}/dev
    install -d ${pkgdir}/etc/default
    ln -s /proc/mounts ${pkgdir}/etc/mtab
    install -d ${pkgdir}/home
    install -d ${pkgdir}/include
    install -d ${pkgdir}/lib/modules
    install -d ${pkgdir}/local/{bin,include,lib,sbin,share}
    install -d -m 0555 ${pkgdir}/proc
    install -d -m 0750 ${pkgdir}/root
    install -d -m 1777 ${pkgdir}/run
    install -d ${pkgdir}/sbin
    install -d -m 0555 ${pkgdir}/sys
    install -d -m 1777 ${pkgdir}/tmp
    install -d ${pkgdir}/share
    install -d ${pkgdir}/usr
    ln -s ../bin     ${pkgdir}/usr/bin
    ln -s ../include ${pkgdir}/usr/include
    ln -s ../local   ${pkgdir}/usr/local
    ln -s ../sbin    ${pkgdir}/usr/sbin
    ln -s ../share   ${pkgdir}/usr/share
    chmod 0555 ${pkgdir}/usr
    install -d ${pkgdir}/var/{cache,lock,lib,log,mail,spool}
    ln -s /run ${pkgdir}/var/run
    install -d -m 1777 ${pkgdir}/var/tmp
    install -m0644 passwd  ${pkgdir}/etc/passwd
    install -m0644 group   ${pkgdir}/etc/group
    install -m0644 profile ${pkgdir}/etc/profile
    install -m0644 shells  ${pkgdir}/etc/shells
    install -m0644 services  ${pkgdir}/etc/services
    install -m0644 protocols  ${pkgdir}/etc/protocols
}
