# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=nginx
pkgver=1.10.1
pkgrel=1
pkgdesc='An HTTP and reverse proxy server.'
arch=(x86_64)
url='http://nginx.org/'
license=(BSD)
groups=(base)
depends=(s6 execline)
makedepends=(
    pkgconf
    libpcre-dev
    libressl-dev
    zlib-dev
)
options=(upx emptydirs)
changelog=ChangeLog
source=(
    "http://nginx.org/download/nginx-${pkgver}.tar.gz"
    nginx-service
    nginx-log
    nginx.install
    nginx.conf
    99-default
)

sha256sums=(
    1fd35846566485e03c0e318989561c135c598323ff349c503a6c14826487a801
    562826624f8bc98df75abdabcb6e9ce079668bec651dc67a95c34d6bc129a750
    9b4f42034d0130d041dfc8f0fcc262891a7d12a529379c6ca344b4a898248d25
    3ffa02560aeeabf2b23828737c1e5c3d998be2fd1fbc9d6dde15c3401cdb4d7c
    6446799214aa0856c2442bb8864b14c105991f2b96a98908d47db5710c6f896b
    4bedee518691aad64caba2581d617e4ca122dbcb29703df755d45ff7ca53b2f1
)

install=nginx.install


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    CC="gcc -fPIC -static" \
        ./configure \
        --prefix='' \
        --conf-path=/etc/nginx/nginx.conf \
        --pid-path=/var/run/nginx.pid \
        --lock-path=/var/lock/nginx.lock \
        --http-log-path=/proc/self/fd/1 \
        --error-log-path=/proc/self/fd/2 \
        --http-client-body-temp-path=/var/tmp/nginx/client_body \
        --http-proxy-temp-path=/var/tmp/nginx/proxy \
        --http-fastcgi-temp-path=/var/tmp/nginx/fastcgi \
        --http-uwsgi-temp-path=/var/tmp/nginx/uwsgi \
        --http-scgi-temp-path=/var/tmp/nginx/scgi \
        --user=nginx \
        --group=nogroup \
        --with-threads
    make $MAKEFLAGS
}

package() {
    pkgfiles=
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" install

    # Share files
    install -d "${pkgdir}"/share/nginx
    mv "${pkgdir}"/html "${pkgdir}"/share/nginx/

    # Configuration
    install -d "${pkgdir}/etc/nginx/conf.d"
    install -d "${pkgdir}/etc/nginx/sites-available"
    install -d "${pkgdir}/etc/nginx/sites-enabled"
    install -m 0644 "${srcdir}/99-default" "${pkgdir}/etc/nginx/sites-available/"
    install -m 0644 "${srcdir}/nginx.conf" "${pkgdir}/etc/nginx/"

    # Service files
    install -d "${pkgdir}/etc/s6/services/available/nginx/log"
    install -m 0754 "${srcdir}/nginx-service" \
        "${pkgdir}/etc/s6/services/available/nginx/run"
    install -m 0754 "${srcdir}/nginx-log" \
        "${pkgdir}/etc/s6/services/available/nginx/log/run"

    # Cleanup
    find "$pkgdir"/etc -name "*.default" -delete
    rm -rf "$pkgdir/var"
    rm -rf "$pkgdir/proc"
}
