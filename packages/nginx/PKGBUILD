pkgname=nginx
pkgver=1.8.0
pkgrel=1
pkgdesc='An HTTP and reverse proxy server.'
arch=(x86_64)
url='http://nginx.org/'
license=(BSD)
groups=(base)
depends=(s6 execline)
makedepends=(
    pkgconf
    libpcre-dev
    openssl-dev
    zlib-dev
)
options=(upx emptydirs)
changelog=ChangeLog
source=(
    "http://nginx.org/download/nginx-${pkgver}.tar.gz"
    nginx-service
    nginx-log
    nginx.install
    nginx.conf
    99-default
)

sha256sums=(
    23cca1239990c818d8f6da118320c4979aadf5386deda691b1b7c2c96b9df3d5
    3a62b6f4cf05a308f66214e09e8b64dde4a1fe04a78ec0e3cdecc927cf882fae
    14da3ca97bcfe51299f007119350b3c318a5158a1e6697cbf23be909f982fd21
    c5446f20588107cfbe1a498601e02f9e18e72119078d09b8d11b657dff21948b
    6446799214aa0856c2442bb8864b14c105991f2b96a98908d47db5710c6f896b
    4bedee518691aad64caba2581d617e4ca122dbcb29703df755d45ff7ca53b2f1
)

install=nginx.install


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    CC="gcc -fPIC -static" \
        ./configure \
        --prefix='' \
        --conf-path=/etc/nginx/nginx.conf \
        --pid-path=/var/run/nginx.pid \
        --lock-path=/var/lock/nginx.lock \
        --http-log-path=/proc/self/fd/1 \
        --error-log-path=/proc/self/fd/2 \
        --http-client-body-temp-path=/var/tmp/nginx/client_body \
        --http-proxy-temp-path=/var/tmp/nginx/proxy \
        --http-fastcgi-temp-path=/var/tmp/nginx/fastcgi \
        --http-uwsgi-temp-path=/var/tmp/nginx/uwsgi \
        --http-scgi-temp-path=/var/tmp/nginx/scgi \
        --user=nginx \
        --group=nogroup \
        --with-threads
    make $MAKEFLAGS
}

package() {
    pkgfiles=
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" install

    # Share files
    install -d "${pkgdir}"/share/nginx
    mv "${pkgdir}"/html "${pkgdir}"/share/nginx/

    # Configuration
    install -d "${pkgdir}/etc/nginx/conf.d"
    install -d "${pkgdir}/etc/nginx/sites-available"
    install -d "${pkgdir}/etc/nginx/sites-enabled"
    install -m 0644 "${srcdir}/99-default" "${pkgdir}/etc/nginx/sites-available/"
    install -m 0644 "${srcdir}/nginx.conf" "${pkgdir}/etc/nginx/"

    # Service files
    install -d "${pkgdir}/etc/s6-services/available/nginx/log"
    install -m 0754 "${srcdir}/nginx-service" \
        "${pkgdir}/etc/s6-services/available/nginx/run"
    install -m 0754 "${srcdir}/nginx-log" \
        "${pkgdir}/etc/s6-services/available/nginx/log/run"

    # Cleanup
    find "$pkgdir"/etc -name "*.default" -delete
    rm -rf "$pkgdir/var"
    rm -rf "$pkgdir/proc"
}
