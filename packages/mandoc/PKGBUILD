# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=mandoc
pkgver=1.14.4
pkgrel=1
pkgdesc='UNIX manpage tools'
arch=(x86_64)
url='http://mdocml.bsd.lv/'
license=(ISC)
groups=(base)
depends=()
makedepends=(
    zlib-dev
)
options=()
changelog=ChangeLog
source=(
    "http://mdocml.bsd.lv/snapshots/${pkgname}-${pkgver}.tar.gz"
    configure.local
)

sha256sums=(
    24eb72103768987dcc63b53d27fdc085796330782f44b3b40c4660b1e1ee9b9c
    76f8831038e8102febf42427dc4a0796f63b3e4e026e13c93fd916d07a5fba4d
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    cp "${srcdir}/configure.local" .
    echo "CFLAGS=\"${CFLAGS} \${CFLAGS}\"" >>configure.local

    # Busybox less doesn't support -T
    sed -i '/strdup("-T")/,+1d' main.c

    ./configure
    make
}

package() {
    pkgfiles=(
        bin
        sbin
        share/man
    )
    cd "${srcdir}/${pkgname}-${pkgver}"
    MAKEFLAGS='' make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
