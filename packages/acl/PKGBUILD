# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='LXC and libarchive require libacl'
pkgname=(
    acl
    libacl
    libacl-dev
)
pkgver=2.2.53
pkgrel=1
pkgdesc='A library for manipulating POSIX access control lists'
arch=(x86_64)
url='http://savannah.nongnu.org/projects/acl'
license=(GPL)
groups=(base)
depends=()
makedepends=(gettext libattr-dev)
options=()
changelog=ChangeLog
source=(
    "http://download.savannah.gnu.org/releases/acl/acl-${pkgver}.tar.gz"
)
sha256sums=(
    06be9865c6f418d851ff4494e12406568353b891ffe1f596b34693c387af26c7
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    CFLAGS='-fPIC' ./configure \
      --prefix=''
    make
}

package_acl() {
    pkgfiles=(
        bin
    )
    depends=(
        libacl
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libacl() {
    pkgfiles=(
        lib/lib*.so.*
    )
    depends=(
        libattr
        musl
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libacl-dev() {
    pkgfiles=(
        include
        lib/*.so
        lib/*.a
    )
    depends=(
        libacl
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
