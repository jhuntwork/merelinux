# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(pacman pacman-build libalpm-dev)
pkgver=5.0.1
pkgrel=8
pkgdesc='A lightweight Package Manager'
arch=(x86_64)
url='https://www.archlinux.org/pacman/'
license=(GPL2)
groups=(base)
depends=()
makedepends=(
    gettext
    gpgme-dev
    libacl-dev
    libarchive-dev
    libcurl-dev
    liblzma-dev
    libressl-dev
    libtool
    libz-dev
    pkgconf
)
options=()
changelog=ChangeLog

source=(
    "https://projects.archlinux.org/pacman.git/snapshot/pacman-${pkgver}.tar.gz"
    makepkg.conf
    pacman.conf
)
sha256sums=(
    84599e2ee2158134fd704f1cd681ea46e021165bed4df5f002aa1748caef9e7b
    69a0d0fd9d8812e10541b32aed02d351094b23afd54ac676de878c156b0af37e
    a4c69eb1b3be80292ad3dd1a90b4c4e9c24d6b7fb1235622fbfe4510c484494a
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./autogen.sh
    sed -i '/x-cpio/s@)@|*application/x-empty*)@' scripts/makepkg.sh.in
    sed -i 's@ --apparent-size@@' configure
    CFLAGS='-fPIC' LDFLAGS='-Wl,-static' \
    ./configure \
      --prefix='' \
      --with-gpgme \
      --disable-shared \
      --disable-nls \
      --disable-doc
    make V=1 LIBS='-lm -lz -lssl -lcrypto -llzma -lacl' -j1
}

package_pacman() {
    backup=(etc/pacman.conf)
    pkgfiles=(
        bin/pacman
        bin/pacman-db-upgrade
        bin/pacman-key
        bin/pacman-optimize
        bin/pacsort
        bin/pactree
        bin/pkgdelta
        bin/vercmp
        "etc/pacman.conf*"
        var
    )
    options+=(upx)

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/destdir" install
    cd "${pkgdirbase}/destdir"
    mv etc/pacman.conf{,.example}
    mv etc/makepkg.conf{,.example}
    install -vm 0644 "${srcdir}/pacman.conf" etc/
    install -vm 0644 "${srcdir}/makepkg.conf" etc/
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_pacman-build() {
    backup=(etc/makepkg.conf)
    pkgfiles=(
        bin/cleanupdelta
        bin/makepkg
        bin/makepkg-template
        bin/repo-*
        bin/testpkg
        "etc/makepkg.conf*"
        share
    )
    depends=(
        bash
        curl
        file
        libarchive
        libressl
        pacman
        pixz
        xz
    )
    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_libalpm-dev() {
    pkgfiles=(
        include
        lib/libalpm.a
        lib/pkgconfig
    )
    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
