pkgname=(pacman pacman-build)
pkgver=4.2.1
pkgrel=1
pkgdesc='The pacman Package Manager'
arch=(x86_64)
url='https://www.archlinux.org/pacman/'
license=(GPL2)
groups=(base)
depends=()
makedepends=(
    gettext
    libtool
    libarchive-devel
    libcurl-devel
    liblzma-devel
    openssl-devel
    pkgconf
    zlib-devel
)
options=()
changelog=ChangeLog

source=(
    "https://projects.archlinux.org/pacman.git/snapshot/pacman-${pkgver}.tar.gz"
    makepkg.conf
    pacman.conf
)
sha256sums=(
    6d411509310760acbf0c3cf4d7e72c604b3e7919306681507c9107c527709cc5
    e46794ced658eaa2e64133cd14ac70ac6d6835dd815d435b1e1604e8ecd2732b
    0a3ba7c8f9e6ab3ae2e596149c7238bc9ce80055c645c686eb8a810721ff4399
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./autogen.sh
    sed -i '/x-cpio/s@)@|*application/x-empty*)@' scripts/makepkg.sh.in
    sed -i 's@ --apparent-size@@' configure
    LDFLAGS='-Wl,-static' \
    ./configure \
      --prefix='' \
      --disable-shared \
      --disable-nls \
      --disable-doc
      make V=1 LIBS='-lm -lz -lssl -lcrypto -llzma' $MAKEFLAGS
}

package_pacman() {
    pkgfiles=(
        bin/pacman
        bin/pacman-key
        bin/pacsort
        bin/pacman-db-upgrade
        bin/vercmp
        bin/pkgdelta
        "etc/pacman.conf*"
        var
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    mv ${pkgdir}-tmp/etc/pacman.conf{,.example}
    install -vm 0644 "${srcdir}/pacman.conf" "${pkgdir}-tmp/etc/"
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_pacman-build() {
    pkgfiles=(
        bin/makepkg
        bin/cleanupdelta
        bin/repo-add
        bin/repo-remove
        bin/repo-elephant
        bin/testpkg
        bin/testdb
        "etc/makepkg.conf*"
        share
    )
    depends=(
        bash
        curl
        file
        libarchive
        openssl
        pacman
        xz
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    mv ${pkgdir}-tmp/etc/makepkg.conf{,.example}
    install -vm 0644 "${srcdir}/makepkg.conf" "${pkgdir}-tmp/etc/"
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
