pkgname=(pacman pacman-build)
pkgver=4.2.1
pkgrel=3
pkgdesc='The pacman Package Manager'
arch=(x86_64)
url='https://www.archlinux.org/pacman/'
license=(GPL2)
groups=(base)
depends=()
makedepends=(
    gettext
    gpgme-dev
    libtool
    libarchive-dev
    libcurl-dev
    liblzma-dev
    openssl-dev
    pkgconf
    zlib-dev
)
options=()
changelog=ChangeLog

source=(
    "https://projects.archlinux.org/pacman.git/snapshot/pacman-${pkgver}.tar.gz"
    makepkg.conf
    pacman.conf
)
sha256sums=(
    6d411509310760acbf0c3cf4d7e72c604b3e7919306681507c9107c527709cc5
    e81771f782e27a5e10e076c9e3ba1f93cc796318608c9769bea13e952c53f1de
    a4c69eb1b3be80292ad3dd1a90b4c4e9c24d6b7fb1235622fbfe4510c484494a
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./autogen.sh
    sed -i '/x-cpio/s@)@|*application/x-empty*)@' scripts/makepkg.sh.in
    sed -i 's@ --apparent-size@@' configure
    LDFLAGS='-Wl,-static' \
    ./configure \
      --prefix='' \
      --with-gpgme \
      --disable-shared \
      --disable-nls \
      --disable-doc
      make V=1 LIBS='-lm -lz -lssl -lcrypto -llzma' $MAKEFLAGS
}

package_pacman() {
    pkgfiles=(
        bin/pacman
        bin/pacman-key
        bin/pacsort
        bin/pacman-db-upgrade
        bin/vercmp
        bin/pkgdelta
        "etc/pacman.conf*"
        var
    )
    options+=(
        upx
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    mv ${pkgdir}-tmp/etc/pacman.conf{,.example}
    install -vm 0644 "${srcdir}/pacman.conf" "${pkgdir}-tmp/etc/"
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_pacman-build() {
    pkgfiles=(
        bin/makepkg
        bin/cleanupdelta
        bin/repo-add
        bin/repo-remove
        bin/repo-elephant
        bin/testpkg
        bin/testdb
        "etc/makepkg.conf*"
        share
    )
    depends=(
        bash
        curl
        file
        libarchive
        openssl
        pacman
        xz
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    mv ${pkgdir}-tmp/etc/makepkg.conf{,.example}
    install -vm 0644 "${srcdir}/makepkg.conf" "${pkgdir}-tmp/etc/"
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
