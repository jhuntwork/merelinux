# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='The package manager used for the final system'
pkgname=(pacman pacman-build libalpm-dev)
pkgver=5.1.1
pkgrel=2
pkgdesc='A lightweight Package Manager'
arch=(x86_64)
url='https://www.archlinux.org/pacman/'
license=(GPL2)
groups=(base)
depends=()
makedepends=(
    gettext
    libacl-dev
    libarchive-dev
    libcurl-dev
    liblzma-dev
    libressl-dev
    libtool
    nettle-dev
    zlib-dev
    pkgconf
)
options=()
changelog=ChangeLog

source=(
    "https://projects.archlinux.org/pacman.git/snapshot/pacman-${pkgver}.tar.gz"
    makepkg.conf
    pacman.conf
    fakeroot
    dependencies.sh
)
sha256sums=(
    340a8d3a60d0c26922fa6054455a1b728925a88852cc44711e07536ff049e49d
    59d188dd2b4444f8079add3332c15af6dbc3841fef8ac98e08f7a7857ee58f05
    a4c69eb1b3be80292ad3dd1a90b4c4e9c24d6b7fb1235622fbfe4510c484494a
    689b6064bea140990b6655cba26bc8cb16d1590c090688d169e5c3929d12a1e3
    4d1a3db8ac679e75000fd5d39b9fc2371f5deefe6dffcb3dfcf672eb0b9c3a7c
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i -e 's@/usr/bin/@@g' \
       -e 's@/usr/bin\$PATH_SEPARATOR@@g' \
       -e 's@ --apparent-size@@' configure.ac
    sed -i -e '/x-cpio/s@)@|*application/x-empty*)@' \
        -e 's/EUID == 0/EUID == -1/' \
        scripts/makepkg.sh.in
    ./autogen.sh
    CFLAGS='-fPIC' LDFLAGS='-Wl,-static' \
    ./configure \
      --prefix='' \
      --disable-shared \
      --disable-nls \
      --disable-doc
    make V=1 LIBS='-lm -lz -lnettle -lssl -lcrypto -llzma -lacl'
}

package_pacman() {
    backup=(etc/pacman.conf)
    pkgfiles=(
        bin/pacman
        bin/pacman-db-upgrade
        bin/pacman-key
        bin/vercmp
        etc/pacman.conf*
        var
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    mv etc/pacman.conf{,.example}
    mv etc/makepkg.conf{,.example}
    install -vm 0644 "${srcdir}/pacman.conf" etc/
    install -vm 0644 "${srcdir}/makepkg.conf" etc/
    install -vm 0755 "${srcdir}/fakeroot" bin/
    install -vm 0755 "${srcdir}/dependencies.sh" share/makepkg/lint_package/
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_pacman-build() {
    backup=(etc/makepkg.conf)
    pkgfiles=(
        bin/cleanupdelta
        bin/fakeroot
        bin/makepkg
        bin/makepkg-template
        bin/repo-*
        bin/pkgdelta
        bin/testpkg
        etc/makepkg.conf*
        share
    )
    depends=(
        bash
        curl
        file
        libarchive
        libressl
        nettle
        pacman
        pixz
        xz
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_libalpm-dev() {
    pkgfiles=(
        include
        lib/libalpm.a
        lib/pkgconfig
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
