pkgname=(xz liblzma-devel)
pkgver=5.2.1
pkgrel=2
pkgdesc='Free general-purpose data compression software with high compression ratio.'
arch=('x86_64')
url='http://tukaani.org/xz/'
license=('GPL2')
groups=('base')
makedepends=()
options=()
changelog=ChangeLog

source=(
    "${url}/${pkgname}-${pkgver}.tar.gz"
)
sha256sums=(
    'b918b6648076e74f8d7ae19db5ee663df800049e187259faf5eb997a7b974681'
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    CFLAGS='-fPIC' LDFLAGS='-Wl,-static' \
      ./configure \
      --prefix='' \
      --disable-shared \
      --enable-static \
      --disable-nls \
      --includedir=/include \
      --mandir=/share/man
    make $MAKEFLAGS
}

package_xz() {
    pkgfiles=(
        bin/*
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    # The following two files are provided in busybox
    rm bin/{lzcat,unlzma,unxz,xzcat}
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_liblzma-devel() {
    pkgfiles=(
        include
        lib/liblzma.a
        lib/pkgconfig/liblzma.pc
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
