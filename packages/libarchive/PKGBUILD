pkgname=(
    libarchive
    libarchive-dev
)
pkgver=3.2.2
pkgrel=1
pkgdesc='Multi-format archive and compression library'
arch=(x86_64)
url='http://www.libarchive.org'
license=(BSD)
groups=(base)
depends=()
makedepends=(
    libacl-dev
    libattr-dev
    liblzma-dev
    libressl-dev
    libz-dev
)
options=(!upx)
changelog=ChangeLog
source=(
    "http://www.libarchive.org/downloads/${pkgname}-${pkgver}.tar.gz"
)
sha256sums=(
    691c194ee132d1f0f7a42541f091db811bc2e56f7107e9121be2bc8c04f1060f
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    # fix for musl
    sed -i 's@HAVE_LCHMOD@&_DISABLE@' libarchive/archive_write_disk_posix.c
    # fixes for busybox xz
    sed -i 's@ -qq@@' libarchive/archive_read_support_filter_xz.c
    sed -i 's@xz -d@unxz@' libarchive/archive_read_support_filter_xz.c
    sed -i 's@lzma -d@unlzma@' libarchive/archive_read_support_filter_xz.c
    CFLAGS="-O3 -fPIC $CFLAGS" \
      ./configure \
      --prefix='' \
      --enable-static \
      --enable-shared
    make V=1 $MAKEFLAGS
}

package_libarchive() {
    pkgfiles=(
        bin
        lib/*.so.*
        share/man/man1
        share/man/man5/libarchive-formats*
    )
    depends=(
        libacl
        libattr
        libressl
        libz
        musl
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/destdir" install

    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_libarchive-dev() {
    depends=(libarchive)
    pkgfiles=(
        include
        lib/*.a
        lib/*.so
        lib/pkgconfig
        share/man/man3
    )
    depends=(libarchive)

    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
