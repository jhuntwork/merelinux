pkgname=binutils
pkgver=2.27
pkgrel=1
pkgdesc='A collection of binary tools, including a linker and an assembler'
arch=('x86_64')
url='http://www.gnu.org/software/binutils/'
license=('GPL3')
groups=('base')
depends=()
makedepends=(
    bison
    gcc-c++
    libz-dev
)
options=()
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
)

sha256sums=(
    369737ce51587f92466041a97ab7d2358c6d9e1b6490b3940eb09fb0a9a6ac88
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i -e 's/linux-gnu/linux-musl/g' \
        -e 's@LIBC=gnu@LIBC=musl@' \
        $(find . -name "confi*.guess" -o -name "confi*.sub")
    rm -f etc/standards.info
    sed -i '/^INFO/s/standards.info //' etc/Makefile.in
    mkdir "../${pkgname}-build"
    cd "../${pkgname}-build"
    LDFLAGS="-Wl,-static" \
      ../${pkgname}-${pkgver}/configure \
      --prefix='' \
      --disable-shared \
      --disable-werror \
      --disable-nls \
      --mandir=/share/man \
      --disable-install-libbfd \
      --with-system-zlib
    echo "MAKEINFO = :" >> Makefile
    make $MAKEFLAGS tooldir=/
}

check() {
    cd "${srcdir}/${pkgname}-build"
    MAKEFLAGS='' make LDFLAGS='' check
}

package() {
    cd "${srcdir}/${pkgname}-build"
    make DESTDIR="$pkgdir" tooldir=/ install-gas install-ld install-binutils
    rm -rf "${pkgdir}/info"
}
