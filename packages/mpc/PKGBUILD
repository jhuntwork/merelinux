# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(mpc mpc-dev)
pkgver=1.1.0
pkgrel=1
pkgdesc='A library for the arithmetic of complex numbers'
arch=('x86_64')
url='http://www.multiprecision.org'
license=(GPL)
groups=(base)
depends=()
makedepends=(gmp-dev mpfr-dev)
options=()
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
)
sha256sums=(
    6985c538143c1208dcb1ac42cedad6ff52e267b47e5f970183a3e75125b43c2e
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure --prefix='' \
      --enable-thread-safe
    make
    make check
}

package_mpc() {
    pkgfiles=(
        lib/libmpc.so.*
    )
    depends=(musl mpfr gmp)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_mpc-dev() {
    pkgfiles=(
        include
        lib/libmpc.a
        lib/libmpc.so
    )
    depends=(mpc)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
