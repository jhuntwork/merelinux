# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(gmp gmp-dev)
pkgver=6.1.2
pkgrel=1
pkgdesc='A free library for arbitrary precision arithmetic.'
arch=('x86_64')
url='http://gmplib.org'
license=(LGPL BSD)
groups=(base)
depends=()
makedepends=(gcc-c++)
options=()
changelog=ChangeLog
source=(
    "https://gmplib.org/download/gmp/${pkgname}-${pkgver}.tar.xz"
)
sha256sums=(
    87b565e89a9a684fe4ebeeddb8399dce2599f9c9049854ca8c0dfbdea0e21912
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure --prefix='' \
      --build=$CHOST \
      --host=$CHOST \
      --target=$CHOST \
      --enable-cxx
    make
}

package_gmp() {
    pkgfiles=(
        lib/libgmp.so.*
        lib/libgmpxx.so.*
    )
    depends=(musl)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -pudmv "${pkgdir}"
}

package_gmp-dev() {
    pkgfiles=(
        include
        lib/libgmp.so
        lib/libgmp.a
        lib/libgmpxx.so
        lib/libgmpxx.a
    )
    depends=(gmp)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -pudmv "${pkgdir}"
}
