# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(
    curl
    libcurl
    libcurl-dev
)
pkgver=7.51.0
pkgrel=1
pkgdesc='An API for writing text-based user interfaces.'
arch=(x86_64)
url='http://curl.haxx.se'
license=(GPL2)
groups=(base)
depends=()
makedepends=(libressl-dev perl libz-dev)
options=()
changelog=ChangeLog

source=(
    "${url}/download/${pkgname}-${pkgver}.tar.gz"
)
sha256sums=(
    65b5216a6fbfa72f547eb7706ca5902d7400db9868269017a8888aa91d87977c
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    rm src/tool_hugehelp.c
    sed -i 's@sys/poll.h@poll.h@g' $(grep -lr 'sys/poll.h' .)
    CFLAGS="$CFLAGS -fPIC" \
    LDFLAGS='-Wl,-static' \
    ./configure \
      --prefix=/ \
      --enable-static \
      --disable-shared \
      --with-ssl \
      --with-ca-bundle=/etc/ssl/ca-certs.pem
    make $MAKEFLAGS V=1
    install -v src/curl{,-static}
    unset LDFLAGS
    make clean
    CFLAGS="$CFLAGS -fPIC" \
    ./configure \
      --prefix=/ \
      --enable-static \
      --enable-shared \
      --with-ssl \
      --with-ca-bundle=/etc/ssl/ca-certs.pem
    make $MAKEFLAGS V=1
}

package_curl() {
    pkgfiles=(
        bin/curl
    )
    depends=(
        ca-certs
    )
    options=(upx)

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    install -m0755 -v src/curl-static "${pkgdir}-tmp/bin/curl"
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_libcurl() {
    pkgfiles=(
        "lib/libcurl.so.*"
    )
    depends=(
        ca-certs
        musl
        libressl
        libz
    )


    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_libcurl-dev() {
    pkgfiles=(
        bin/curl-config
        include
        "lib/*.a"
        "lib/*.so"
        lib/pkgconfig
        share/aclocal
    )
    depends=(
        libcurl
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
