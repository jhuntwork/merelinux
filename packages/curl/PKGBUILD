pkgname=(
    curl
    libcurl
    libcurl-dev
)
pkgver=7.43.0
pkgrel=1
pkgdesc='An API for writing text-based user interfaces.'
arch=(x86_64)
url='http://curl.haxx.se'
license=(GPL2)
groups=(base)
depends=()
makedepends=(
    openssl-dev
    perl
    zlib-dev
)
options=()
changelog=ChangeLog

source=(
    "${url}/download/${pkgname}-${pkgver}.tar.gz"
)
sha256sums=(
    1a084da1edbfc3bd632861358b26af45ba91aaadfb15d6482de55748b8dfc693
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    rm src/tool_hugehelp.c
    CFLAGS="$CFLAGS -fPIC" \
    LDFLAGS='-Wl,-static' \
    ./configure \
      --prefix=/ \
      --enable-static \
      --disable-shared \
      --with-ssl \
      --with-ca-bundle=/etc/certs/ca-bundle.crt
    make $MAKEFLAGS
    install -v src/curl{,-static}
    unset LDFLAGS
    make clean
    CFLAGS="$CFLAGS -fPIC" \
    ./configure \
      --prefix=/ \
      --enable-static \
      --enable-shared \
      --with-ssl \
      --with-ca-bundle=/etc/certs/ca-bundle.crt
    make $MAKEFLAGS
}

package_curl() {
    pkgfiles=(
        bin/curl
    )
    depends=(
        ca-certificates
    )
    options=(upx)

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    install -m0755 -v src/curl-static "${pkgdir}-tmp/bin/curl"
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_libcurl() {
    pkgfiles=(
        "lib/libcurl.so.*"
    )
    depends=(
        musl
        openssl
        zlib
    )


    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_libcurl-dev() {
    pkgfiles=(
        bin/curl-config
        include
        "lib/*.a"
        "lib/*.so"
        lib/pkgconfig
        share/aclocal
    )
    depends=(
        libcurl
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
