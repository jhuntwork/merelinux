# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='LXC and libarchive require libattr'
pkgname=(
    attr
    libattr
    libattr-dev
)
pkgver=2.4.48
pkgrel=1
pkgdesc='A library for manipulating filesystem extended attributes'
arch=(x86_64)
url='http://savannah.nongnu.org/projects/attr'
license=(GPL)
groups=(base)
depends=()
makedepends=(gettext)
options=()
changelog=ChangeLog
source=(
    "http://download.savannah.gnu.org/releases/attr/attr-${pkgver}.tar.gz"
)
sha256sums=(
    5ead72b358ec709ed00bbf7a9eaef1654baad937c001c044fe8b74c57f5324e7
)


build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure \
      --prefix=''
    make
}

package_attr() {
    pkgfiles=(
        bin
    )
    depends=(
        libattr
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libattr() {
    pkgfiles=(
        lib/*.so.*
    )
    depends=(
        musl
    )

    cd "${pkgdirbase}/dest"
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libattr-dev() {
    pkgfiles=(
        include
        lib/*.a
        lib/*.so
    )
    depends=(
        libattr
    )

    cd "${pkgdirbase}/dest"
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
