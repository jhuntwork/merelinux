# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(sqlite libsqlite libsqlite-dev)
pkgver=3.18.0
_pkgaltver=3180000
pkgrel=1
pkgdesc='A self-contained embedded SQL database engine.'
arch=('x86_64')
url='https://sqlite.org/'
license=(PublicDomain)
groups=('base')
depends=()
makedepends=(readline-dev)
options=()
changelog=ChangeLog

source=(
    "https://sqlite.org/2017/sqlite-autoconf-${_pkgaltver}.tar.gz"
)

sha256sums=(
    3757612463976e7d08c5e9f0af3021613fc24bbcfe1c51197d6776b9ece9ac5c
)


build() {
    cd "${srcdir}/sqlite-autoconf-${_pkgaltver}"
    CFLAGS="$CFLAGS -fPIC -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1"
    CFLAGS="$CFLAGS -DSQLITE_ENABLE_UNLOCK_NOTIFY=1 -DSQLITE_SECURE_DELETE=1"
    CFLAGS="$CFLAGS -DSQLITE_ENABLE_DBSTAT_VTAB=1 --static" \
        ./configure --prefix=''
    make
}

package_sqlite() {
    pkgfiles=(
        bin
        share/man/man1
    )

    cd "${srcdir}/${pkgbase}-autoconf-${_pkgaltver}"
    make DESTDIR="${pkgdirbase}/destdir" install

    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libsqlite() {
    pkgfiles=(
        lib/*.so.*
    )
    depends=(
        musl
        readline
    )
    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libsqlite-dev() {
    pkgfiles=(
        include
        lib/*.a
        lib/*.so
        lib/pkgconfig
    )
    depends=(
        libsqlite
    )

    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
