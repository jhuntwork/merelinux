# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(
    e2fsprogs
    libuuid-dev
    libblkid-dev
)
pkgver=1.42.13
pkgrel=1
pkgdesc='Filesystem utilities for ext2, ext3 and ext4.'
arch=(x86_64)
url=http://e2fsprogs.sourceforge.net/
license=(GPL)
groups=(base)
depends=()
makedepends=()
options=()
changelog=ChangeLog
source=(
    "https://www.kernel.org/pub/linux/kernel/people/tytso/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.xz"
)

sha256sums=(
    e16474b5a3a30f9197160c4b91bd48d5a463583049c0fcc405b6f0f7075aa0c7
)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir build
    cd build
    CFLAGS='-fPIC -static' \
      LDFLAGS='-static' \
      ../configure --prefix=''
    make V=1 $MAKEFLAGS
}

package_e2fsprogs() {
    options=(upx)
    pkgfiles=(
        bin/uuidgen
        bin/chattr
        bin/lsattr
        etc/mke2fs.conf
        sbin/
        sbin/e4defrag
        sbin/logsave
        sbin/fsck.ext4
        sbin/mkfs.ext2
        sbin/findfs
        sbin/fsck
        sbin/blkid
        sbin/debugfs
        sbin/badblocks
        sbin/fsck.ext4dev
        sbin/mklost+found
        sbin/mke2fs
        sbin/mkfs.ext4
        sbin/dumpe2fs
        sbin/e2label
        sbin/fsck.ext3
        sbin/resize2fs
        sbin/filefrag
        sbin/mkfs.ext3
        sbin/e2image
        sbin/e2fsck
        sbin/tune2fs
        sbin/fsck.ext2
        sbin/e2undo
        sbin/uuidd
        sbin/mkfs.ext4dev
        sbin/e2freefrag
    )
    cd "${srcdir}/${pkgbase}-${pkgver}/build"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libuuid-dev() {
    pkgfiles=(
        include/uuid
        lib/pkgconfig/uuid.pc
        lib/libuuid.a
    )
    depends=(pkgconf)
    cd "${srcdir}/${pkgbase}-${pkgver}/build"
    make DESTDIR="${pkgdir}-tmp" install-libs
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libblkid-dev() {
    pkgfiles=(
        include/blkid
        lib/pkgconfig/blkid.pc
        lib/libblkid.a
    )
    depends=(pkgconf libuuid-dev)
    cd "${srcdir}/${pkgbase}-${pkgver}/build"
    make DESTDIR="${pkgdir}-tmp" install-libs
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
