# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(e2fsprogs libblkid-dev libuuid-dev)
pkgver=1.44.4
pkgrel=1
pkgdesc='Filesystem utilities for ext2, ext3 and ext4.'
arch=(x86_64)
url=http://e2fsprogs.sourceforge.net/
license=(GPL)
groups=(base)
depends=()
makedepends=(libattr-dev)
options=()
changelog=ChangeLog
source=(
    "https://www.kernel.org/pub/linux/kernel/people/tytso/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.xz"
)

sha256sums=(
    ee03b2f2180614c5ae5f6dd2666f8808de61c3d8b37d22ba86cc6daea25be55a
)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    CFLAGS+=' -fPIC -D__GNUC_PREREQ\(...\)=0' LDFLAGS='-Wl,-static' \
        ac_cv_path_mkdir=mkdir \
        ./configure --prefix=''
    MAKEFLAGS='' make V=1
}

package_e2fsprogs() {
    pkgfiles=(
        bin
        etc/mke2fs.conf
        sbin
        share/man/man1
        share/man/man5
        share/man/man8
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    MAKEFLAGS='' make V=1 DESTDIR="${pkgdirbase}/dest" install install-libs
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libblkid-dev() {
    pkgfiles=(
        include/blkid
        lib/pkgconfig/blkid.pc
        lib/libblkid.a
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libuuid-dev() {
    pkgfiles=(
        include/uuid
        lib/pkgconfig/uuid.pc
        lib/libuuid.a
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
