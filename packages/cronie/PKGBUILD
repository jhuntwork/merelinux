pkgname=cronie
pkgver=1.5.1
pkgrel=1
pkgdesc='A standard UNIX daemon crond'
arch=(x86_64)
url='https://fedorahosted.org/cronie/'
license=(GPL2)
groups=(base)
depends=()
makedepends=()
options=(upx emptydirs)
changelog=ChangeLog
source=(
    "https://fedorahosted.org/releases/c/r/${pkgname}/${pkgname}-${pkgver}.tar.gz"
    crond-service
)

sha256sums=(
    6c73666102a6b5d35e2eaf1bd06925f2d4b0cef8d3323c37286dda3089a85338
    fd6e2ff2f82087e64103050315cd3d291adedc34322e23f3209e6e9bb4cc1fb4
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    LDFLAGS="-Wl,-static" \
    ./configure \
      --prefix=''\
      --enable-syscrontab
    make $MAKEFLAGS
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" install
    install -d "${pkgdir}/etc/cron.d"
    cd "$pkgdir"
    rm -rf share
    install -d etc/s6/services/available/crond
    install -m 0754 "${srcdir}/crond-service" etc/s6/services/available/crond/run
}
