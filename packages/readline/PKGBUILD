# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(readline readline-dev)
pkgver=7.0
pkgrel=1
pkgdesc='A library for allowing users to edit command lines as they are typed in.'
arch=('x86_64')
url='http://www.gnu.org/software/readline/'
license=('BSD')
groups=('base')
depends=()
makedepends=(libncurses-dev)
options=()
changelog=ChangeLog

PURGE_TARGETS+=(share/info/* share/readline/*)

source=(
    "http://ftp.gnu.org/gnu/readline/${pkgname}-${pkgver}.tar.gz"
)

sha256sums=(
    750d437185286f40a369e1e4f4764eda932b9459b5ec9a731628393dd3d32334
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i '/MV.*old/d' Makefile.in
    sed -i '/{OLDSUFF}/c:' support/shlib-install
    CFLAGS='-fPIC' \
    ./configure \
      --prefix=''
    make SHLIB_LIBS=-lncursesw
}

package_readline() {
    pkgfiles=(
        lib/*.so.*
    )
    depends=(
        musl
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/destdir" install
    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_readline-dev() {
    pkgfiles=(
        include
        lib/*.a
        lib/*.so
        share/man/man3
    )
    depends=(
        readline
    )

    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
